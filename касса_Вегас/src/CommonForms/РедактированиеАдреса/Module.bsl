
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств( ЭтаФорма, Параметры, "Этаж, Улица, Подъезд, Квартира, Дом, Город");
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок =  Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоПросмотрФормы") И Параметры.ТолькоПросмотрФормы Тогда
		ТолькоПросмотр = Истина;
		Элементы.Готово.Видимость = Ложь;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	// ОриентацияЭкрана
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	РезультатЗакрытия = Новый Структура;
	РезультатЗакрытия.Вставить("Город",    Город);
	РезультатЗакрытия.Вставить("Дом",      Дом);
	РезультатЗакрытия.Вставить("Квартира", Квартира);
	РезультатЗакрытия.Вставить("Подъезд",  Подъезд);
	РезультатЗакрытия.Вставить("Улица",    Улица);
	РезультатЗакрытия.Вставить("Этаж",     Этаж);
	
	Закрыть(РезультатЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	
	ОбщегоНазначения.НастроитьКомандуГотово(ЭтотОбъект, , , 2);
	
КонецПроцедуры

#КонецОбласти

#Область ОриентацияЭкрана 

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
	ГоризонтальнаяОриентацияЭкрана = ПараметрыСеанса.ГоризонтальнаяОриентацияЭкрана;
	
	СписокПолей = "Город, Дом, Квартира, Подъезд, Улица, Этаж";
	
	Если ПараметрыСеанса.ГоризонтальнаяОриентацияЭкрана Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма,
			СписокПолей,
			"ПоложениеЗаголовка",
			ПоложениеЗаголовкаЭлементаФормы.Лево
		);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма,
			СписокПолей,
			"ГоризонтальноеПоложение",
			ГоризонтальноеПоложениеЭлемента.Право
		);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма,
			СписокПолей,
			"ПоложениеЗаголовка",
			ПоложениеЗаголовкаЭлементаФормы.Верх
		);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма,
			СписокПолей,
			"ГоризонтальноеПоложение",
			ГоризонтальноеПоложениеЭлемента.Лево
		);
	
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти
