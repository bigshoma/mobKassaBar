
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	УстановленныйПароль = Константы.ПарольАдминистративногоРежима.Получить();
	
	Если НЕ ЗначениеЗаполнено(УстановленныйПароль) Тогда
		
		Элементы.ГруппаСтарыйПароль.Видимость = Ложь;
		
	КонецЕсли;
	
	// ОриентацияЭкрана
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнениеФормы() Тогда
		
		Если НЕ СтарыйПароль = УстановленныйПароль Тогда
			
			ТекстСообщения = НСтр("ru = 'Старый и установленный пароль не совпадают'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
			
		КонецЕсли;
			
		Если НовыйПароль = НовыйПарольПодтверждение Тогда
			
			СохранитьНаСервере();
			ПараметрыЗакрытия = "УстановленПароль";
			Оповестить("ИзмененАдминистративныйРежим");
			Закрыть(ПараметрыЗакрытия);
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Пароль и подтверждение пароля не совпадают'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСкрытьПароль(Команда)
	
	ПарольОтображается = НЕ ПарольОтображается;
	НастроитьОтображениеПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСкрытьНовыйПароль(Команда)
	
	НовыйПарольОтображается = НЕ НовыйПарольОтображается;
	НастроитьОтображениеПароля();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверитьЗаполнениеФормы()
	
	Если ЗначениеЗаполнено(УстановленныйПароль) И НЕ ЗначениеЗаполнено(СтарыйПароль) Тогда
		
		ПомощникUIКлиент.СообщитьПолеНеЗаполнено(ЭтотОбъект, Элементы.СтарыйПароль, НСтр("ru = 'Старый пароль'"));
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	
	ОбщегоНазначения.НастроитьКомандуГотово(ЭтотОбъект, , , 2);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	Константы.ПарольАдминистративногоРежима.Установить(НовыйПароль);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтображениеПароля()
	
	Если ПарольОтображается Тогда
		
		Элементы.СтраницыПароль.ТекущаяСтраница = Элементы.СтраницаПарольГлазПеречеркнутый;
		
	Иначе
		
		Элементы.СтраницыПароль.ТекущаяСтраница = Элементы.СтраницаПарольГлаз;
		
	КонецЕсли;
	
	Если НовыйПарольОтображается Тогда
		
		Элементы.СтраницыНовыйПароль.ТекущаяСтраница = Элементы.СтраницаНовыйПарольГлазПеречеркнутый;
		
	Иначе
		
		Элементы.СтраницыНовыйПароль.ТекущаяСтраница = Элементы.СтраницаНовыйПарольГлаз;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОриентацияЭкрана 

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
КонецПроцедуры

#КонецОбласти
