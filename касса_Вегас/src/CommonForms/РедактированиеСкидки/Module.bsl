
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТолькоПросмотрФормы = Параметры.ТолькоПросмотрФормы;
	ЗаголовокДокумента = Параметры.ЗаголовокДокумента;
	
	// Оформление
	ОбщегоНазначения.ОформитьСуммовыеПоля(ЭтаФорма, "Сумма, СуммаБезСкидкиНаценки, СкидкаНаценкаПроцент, СкидкаНаценкаСумма");
	ОбщегоНазначения.УстановитьЦветИтоговыхПолей(ЭтаФорма, "Сумма, СуммаБезСкидкиНаценки");
	
	НастроитьФормуПоЗначениямНастроек();
	
	ОформитьСтатичныеЗаголовкиИПоля();
	
	// ОриентацияЭкрана
	НастроитьФормуПоОриентацииЭкрана();
	
	СкидкаНаценка = 1;
	
	ЗначениеСкидки = 1;
	ЗначениеНаценки = 2;
	
	СкидкаНаценкаПроцент  = ?(Параметры.СкидкаНаценкаПроцент < 0, - Параметры.СкидкаНаценкаПроцент, Параметры.СкидкаНаценкаПроцент);
	СкидкаНаценкаСумма    = ?(Параметры.СкидкаНаценкаСумма < 0, - Параметры.СкидкаНаценкаСумма, Параметры.СкидкаНаценкаСумма);
	СуммаБезСкидкиНаценки = Параметры.СуммаБезСкидкиНаценки;
	
	СкидкаНаценка = ?(Параметры.СкидкаНаценкаПроцент > 0, ЗначениеНаценки, ЗначениеСкидки);
	
	НастроитьФормуПоСкидкеНаценке();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОпределитьМаксимальныеЗначенияСкидкиНаценки();
	РассчитатьСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыЗначенияНастроек" Тогда
		
		НастроитьФормуПоЗначениямНастроек();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура СкидкаНаценкаПриИзменении(Элемент)
	
	ОпределитьМаксимальныеЗначенияСкидкиНаценки();
	РассчитатьСуммы();
	
	НастроитьФормуПоСкидкеНаценке();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	
	ПараметрыЗакрытия.Вставить("СкидкаНаценкаПроцент", ?(СкидкаНаценка = ЗначениеСкидки, - СкидкаНаценкаПроцент, СкидкаНаценкаПроцент));
	ПараметрыЗакрытия.Вставить("СкидкаНаценкаСумма",   ?(СкидкаНаценка = ЗначениеСкидки, - СкидкаНаценкаСумма,   СкидкаНаценкаСумма));
	ПараметрыЗакрытия.Вставить("Сумма",                Сумма);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СкидкаНаценкаПроцентПриИзменении(Элемент)
	
	СкидкаНаценкаСумма = СкидкаНаценкаПроцент * СуммаБезСкидкиНаценки /100;
	
	РассчитатьСуммы();

КонецПроцедуры

&НаКлиенте
Процедура СкидкаНаценкаСуммаПриИзменении(Элемент)
	
	СкидкаНаценкаПроцент = СкидкаНаценкаСумма * 100 / СуммаБезСкидкиНаценки;
	
	РассчитатьСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммы()
	
	Сумма = СуммаБезСкидкиНаценки - ?(СкидкаНаценка = ЗначениеСкидки, СкидкаНаценкаСумма, - СкидкаНаценкаСумма);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоСкидкеНаценке()
	
	Если СкидкаНаценка = 1 Тогда // Скидка
		
		Элементы.СкидкаНаценкаСумма.Заголовок = НСтр("ru = 'Скидка'");
		Элементы.СкидкаНаценкаПроцент.Заголовок = НСтр("ru = 'Скидка %'");
		
		Элементы.СуммаБезСкидкиНаценки.Заголовок = НСтр("ru = 'Без скидки'");
		
		ЗаголовокФормы = НСтр("ru = 'Скидка на %ЗаголовокДокумента%'");
		ЭтаФорма.Заголовок = СтрЗаменить(ЗаголовокФормы, "%ЗаголовокДокумента%", ЗаголовокДокумента);
		
	ИначеЕсли СкидкаНаценка = 2 Тогда // Наценка
		
		Элементы.СкидкаНаценкаСумма.Заголовок = НСтр("ru = 'Наценка'");
		Элементы.СкидкаНаценкаПроцент.Заголовок = НСтр("ru = 'Наценка %'");
		
		Элементы.СуммаБезСкидкиНаценки.Заголовок = НСтр("ru = 'Без наценки'");
		
		ЗаголовокФормы = НСтр("ru = 'Наценка на %ЗаголовокДокумента%'");
		ЭтаФорма.Заголовок = СтрЗаменить(ЗаголовокФормы, "%ЗаголовокДокумента%", ЗаголовокДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьМаксимальныеЗначенияСкидкиНаценки()
	
	Если СкидкаНаценка = ЗначениеСкидки Тогда
		
		МаксимальноеЗначениеПроцент = 99.99;
		Элементы.СкидкаНаценкаПроцент.МаксимальноеЗначение = МаксимальноеЗначениеПроцент;
		
		ОднаКопейка = 0.01;
		МаксимальноеЗначениеСумма = СуммаБезСкидкиНаценки - ОднаКопейка;
		Элементы.СкидкаНаценкаСумма.МаксимальноеЗначение = МаксимальноеЗначениеСумма;
		
	ИначеЕсли СкидкаНаценка = ЗначениеНаценки Тогда
		
		МаксимальноеЗначениеПроцент = 9999999999999.99;
		Элементы.СкидкаНаценкаПроцент.МаксимальноеЗначение = МаксимальноеЗначениеПроцент;
		
		МаксимальноеЗначениеСумма = 9999999999999.99;
		Элементы.СкидкаНаценкаСумма.МаксимальноеЗначение = МаксимальноеЗначениеСумма;
		
	КонецЕсли;
	
	Если СкидкаНаценкаСумма > МаксимальноеЗначениеСумма Тогда
		
		СкидкаНаценкаСумма = МаксимальноеЗначениеСумма;
		СкидкаНаценкаСуммаПриИзменении(Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьСтатичныеЗаголовкиИПоля()
	
	Если ТолькоПросмотрФормы Тогда
		
		Элементы.Отмена.Заголовок = НСтр("ru = 'Закрыть'");
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "Готово", "Видимость", Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "СкидкаНаценкаПроцент", "КнопкаРегулирования", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "СкидкаНаценкаПроцент", "ТолькоПросмотр", Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "СкидкаНаценкаСумма", "ТолькоПросмотр", Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "СкидкаНаценка", "ТолькоПросмотр", Истина);
		
	КонецЕсли;
	
	
	ОбщегоНазначения.УстановитьЖирныйШрифтПолей(ЭтаФорма, "Сумма");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	
	ОбщегоНазначения.НастроитьКомандуГотово(ЭтотОбъект, , , 2);
	
КонецПроцедуры

#КонецОбласти

#Область ОриентацияЭкрана

&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
	
КонецПроцедуры

#КонецОбласти

