
#Область ПрограммныйИнтерфейс

Функция СтруктураПисьмаОтчета() Экспорт
	
	ПисьмоОтчета = Новый Структура;
	ПисьмоОтчета.Вставить("ТемаПисьма",  "");
	ПисьмоОтчета.Вставить("ТекстПисьма", "");
	ПисьмоОтчета.Вставить("АдресаФайловВложения", Новый СписокЗначений);
	
	Возврат ПисьмоОтчета;
	
КонецФункции

Функция ЗаписатьТабДокВФайлПисьма(ТабДок, Знач ИмяФайла) Экспорт
	
	РезультатЗаписи = Новый Структура;
	
	ИмяФайла = СтрЗаменить(ИмяФайла, " ", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, ".", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, ":", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, "[", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, "]", "_");
	
	ИмяФайла = СтрЗаменить(ИмяФайла, "__", "_");
	
	Если Лев(ИмяФайла, 1) = "_" Тогда
		ИмяФайла = Сред(ИмяФайла, 2);
	КонецЕсли;
	
	ИмяФайла = ИмяФайла + ".pdf";
	
	ВременныйКаталог = КаталогВременныхФайлов();
	
	ПутьФайла = ВременныйКаталог + ИмяФайла;
	
	ТабДок.Записать(ПутьФайла, ТипФайлаТабличногоДокумента.PDF);
	
	РезультатЗаписи.Вставить("ПутьФайла", ПутьФайла);
	РезультатЗаписи.Вставить("ИмяФайла",  ИмяФайла);
	
	Возврат РезультатЗаписи;
	
КонецФункции

Процедура ДобавитьЗаписанныйФайлВоВложениеПисьма(СтруктураПисьма, ЗаписанныйФайл) Экспорт
	
	СтруктураПисьма.АдресаФайловВложения.Добавить(ЗаписанныйФайл.ПутьФайла, ЗаписанныйФайл.ИмяФайла);
	
КонецПроцедуры

Процедура ОтправитьОтчет(СтруктураПисьма) Экспорт
	
	Получатели = РаботаСПочтойВызовСервера.ПолучателиОтчетовПоПочте();
	
	ОтправитьЭлектронноеПисьмо(СтруктураПисьма, Получатели);
	
КонецПроцедуры

Процедура ОтправитьЭлектронноеПисьмо(СтруктураПисьма, Получатели) Экспорт
	
	Сообщение = Новый ИнтернетПочтовоеСообщение;
	
	Для Каждого Получатель Из Получатели Цикл
		Адрес = Сообщение.Получатели.Добавить(Получатель);
	КонецЦикла;
	
	Текст = Сообщение.Тексты.Добавить(СтруктураПисьма.ТекстПисьма);
	Текст.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст;
	Сообщение.Тема  = СтруктураПисьма.ТемаПисьма;
	
	Для Каждого Вложение Из СтруктураПисьма.АдресаФайловВложения Цикл
		Сообщение.Вложения.Добавить(Вложение.Значение, Вложение.Представление);
	КонецЦикла;
	
	#Если МобильноеПриложениеКлиент Тогда
		СредстваПочты.Послать(Сообщение);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти
