
#Область ПрограммныйИнтерфейс

// Рассчитывает сумму НДС от суммы
//
// Сумма      - Число - Сумма, от которой необходимо рассчитать сумму НДС.
// СтавкаНДС  - Ставка НДС числом
//
Функция РассчитатьСуммуНДС(Сумма, ПроцентНДС) Экспорт
	
	СуммаНДС = Сумма * ПроцентНДС / (ПроцентНДС + 1);
	Возврат Окр(СуммаНДС, 2);
	
КонецФункции

Функция ПолучитьСтавку_18_20(СтавкаНДС) Экспорт
	
	ДатаПерехода = ПолучитьДатуПереходаНДС20();
	ТекущаяДата = ОбщегоНазначенияКлиентСервер.ПолучитьТекущуюДату();
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18") Тогда
		
		Если ДатаПерехода <= ТекущаяДата Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");
		Иначе
			Возврат СтавкаНДС;
		КонецЕсли;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118") Тогда
		
		Если ДатаПерехода <= ТекущаяДата Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120");
		Иначе
			Возврат СтавкаНДС;
		КонецЕсли;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20") Тогда
		
		Если ДатаПерехода <= ТекущаяДата Тогда
			Возврат СтавкаНДС;
		Иначе
			Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18");
		КонецЕсли;
			
			
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120") Тогда
		
		Если ДатаПерехода <= ТекущаяДата Тогда
			Возврат СтавкаНДС;
		Иначе
			Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118");
		КонецЕсли;
		
	Иначе
		
		Возврат СтавкаНДС;
	КонецЕсли;
	
КонецФункции

Функция ПрименяетсяСтавкаНДС20(ДатаДокумента = Неопределено) Экспорт
	
	ДатаПерехода = ПолучитьДатуПереходаНДС20();
	
	Если ДатаДокумента = Неопределено Тогда
		ТекущаяДата = ОбщегоНазначенияКлиентСервер.ПолучитьТекущуюДату();
	Иначе
		ТекущаяДата = ДатаДокумента;
	КонецЕсли;
		
	Если ДатаПерехода <= ТекущаяДата Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьРасчетнуюСтавкуНДСАванса(ДатаДокумента = Неопределено) Экспорт
	
	Если ПрименяетсяСтавкаНДС20(ДатаДокумента) Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120");
	Иначе
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118");
	КонецЕсли;
	
КонецФункции

Функция ПолучитьРасчетнуюСтавкуНДСАвансаЧислом(ДатаДокумента = Неопределено) Экспорт
	
	Если ПрименяетсяСтавкаНДС20(ДатаДокумента) Тогда
		Возврат 120;
	Иначе
		Возврат 118;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьДатуПереходаНДС20() Экспорт
	
	Возврат Дата("20190101");
	
КонецФункции

#КонецОбласти
