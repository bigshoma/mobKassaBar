
#Область ПрограммныйИнтерфейс

Функция ПолучитьРасчетнуюСтавкуНДС(Знач СтавкаНДС) Экспорт
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10_110") Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10_110");
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118") Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118");
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120") Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120");
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС") Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0") Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0");
		
	КонецЕсли;
	
КонецФункции

// Возвращает числовое значение ставки НДС по значению перечисления
//
// Параметры:
// 		СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС.
//
// Возвращаемое значение:
// 		Число - Значение ставки НДС числом.
//
Функция ПолучитьСтавкуНДСЧислом(Знач СтавкаНДС) Экспорт
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10_110") Тогда
		
		Возврат 0.1;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118") Тогда
		
		Возврат 0.18;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120") Тогда
		
		Возврат 0.20;
		
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

// Возвращает числовое значение ставки НДС по значению перечисления
//
// Параметры:
// 		СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС.
//
// Возвращаемое значение:
// 		Число - Значение ставки НДС числом.
//
Функция ПолучитьСтавкуНДСЦелымЧислом(Знач СтавкаНДС) Экспорт
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10") Тогда
		Возврат 10;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18") Тогда
		Возврат 18;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20") Тогда
		Возврат 20;

	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0") Тогда
		Возврат 0;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС") Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Возвращает значение перечисления ставки НДС по числовому значению
//
//  Параметры:
//  Число - Значение ставки НДС числом.
//
//  Возвращаемое значение:
//  СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС.
//
Функция ПолучитьСтавкуНДС(Знач СтавкаНДСЗначение, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Если СтавкаНДСЗначение = 18 ИЛИ СтавкаНДСЗначение = "18" Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18");
		
	ИначеЕсли СтавкаНДСЗначение = 20 ИЛИ СтавкаНДСЗначение = "20" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");
		
	ИначеЕсли СтавкаНДСЗначение = 10 ИЛИ СтавкаНДСЗначение = "10" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10");
		
	ИначеЕсли СтавкаНДСЗначение = 0 ИЛИ СтавкаНДСЗначение = "0" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0");
		
	ИначеЕсли СтавкаНДСЗначение = Неопределено ИЛИ ВРег(СтавкаНДСЗначение) = "NONE" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
		
	Иначе
		Если ЗначениеПоУмолчанию = Неопределено Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18");
		Иначе
			Возврат ЗначениеПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
КонецФункции

#КонецОбласти
