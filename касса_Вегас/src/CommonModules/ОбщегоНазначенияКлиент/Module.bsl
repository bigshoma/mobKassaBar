
#Область ПрограммныйИнтерфейс

Процедура ПередЗакрытиемФормыНастроек(Форма, Отказ) Экспорт
	
	Если Форма.Модифицированность И НЕ Форма.ОповеститьОбИзмененииЗначенийНастроек Тогда
		
		Отказ = Истина;
		
		ДополнительныеПараметры = Новый Структура("Форма", Форма);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПередЗакрытием", ЭтотОбъект, ДополнительныеПараметры);
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередНачаломРаботыСистемы(Отказ) Экспорт
	
	ПервыйЗапуск = ОбщегоНазначенияВызовСервера.ОбработатьЗапускПриложения();
	
	Если ПервыйЗапуск Тогда
		
		// ПодключаемоеОборудование
		МенеджерОборудованияВызовСервераПереопределяемый.ОбновлениеБиблиотеки();
		// Конец ПодключаемоеОборудование
		
		ОбщегоНазначенияВызовСервера.ОбновитьПриложение();
		
		РезервноеКопированиеКлиент.УстановитьПутьРезервногоКопированияПриПервомЗапуске();
		
		ОбщегоНазначенияВызовСервера.УстановитьРежимСинхронизацииДемоверсии();
		
		ОбщегоНазначенияВызовСервера.ОбработатьПервыйЗапуск();
		
		ПодключитьОбработчикОжидания("Подключаемый_НастроитьРежимСинхронизации", 0.2, Истина);
		
	КонецЕсли;
	
	ОбщегоНазначенияВызовСервера.ОбновитьПриложение();
	ОбщегоНазначенияВызовСервера.УстановитьОриентациюЭкрана();
	
КонецПроцедуры

Процедура ОткрытьФормуПереключенияАдминистративногоРежима(Владелец) Экспорт

	// АдминистративныйРежим
	ОткрытьФорму("Обработка.НастройкиПриложения.Форма.АдминистративныйРежим",,ЭтотОбъект);
	
КонецПроцедуры

// Код доступа

Процедура УстановитьИзменитьКодДоступа(ПараметрыФормы, Оповещение) Экспорт
	
	ПараметрыФормы.Вставить("РежимФормы", "ИзменитьКод");
	
	ОткрытьФорму("ОбщаяФорма.ВводУстановкаКодаДоступа", ПараметрыФормы, ,,,, Оповещение);
	
КонецПроцедуры

Процедура ВыключитьКодДоступа(ПараметрыФормы, Оповещение) Экспорт
	
	ПараметрыФормы.Вставить("РежимФормы", "ВыключитьКод");
	
	ОткрытьФорму("ОбщаяФорма.ВводУстановкаКодаДоступа", ПараметрыФормы, ,,,, Оповещение);
	
КонецПроцедуры

Процедура ВвестиКодДоступа(ПараметрыФормы, Оповещение) Экспорт
	
	ПараметрыФормы.Вставить("РежимФормы", "ВвестиКод");
	
	ОткрытьФорму("ОбщаяФорма.ВводУстановкаКодаДоступа", ПараметрыФормы, ,,,, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОповещениеПередЗакрытием(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Форма.СохранитьНаКлиенте();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		Форма.Модифицированность = Ложь;
		Форма.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
