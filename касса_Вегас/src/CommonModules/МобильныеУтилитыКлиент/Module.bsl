
#Область ПрограммныйИнтерфейс

#Область Инициализация

Процедура Инициализировать() Экспорт
	
	#Если МобильноеПриложениеКлиент Тогда
		
		НачатьУстановкуВнешнейКомпоненты(, "ОбщийМакет.MobileUtils");
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеПодключенияКомпоненты",
			ЭтотОбъект
		);
		
		НачатьПодключениеВнешнейКомпоненты(
			ОписаниеОповещения,
			"ОбщийМакет.MobileUtils",
			"com_1c_MobileUtils",
			ТипВнешнейКомпоненты.Native
		);
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область МобильныеУтилиты_API

Процедура СоздатьZIP(СписокФайлов, ПолныйПутьZipФайла) Экспорт
	
	#Если МобильноеПриложениеКлиент Тогда
		
		ВнешняяКомпонентаMobileUtils = ПолучитьОбъектКомпоненты();
		
		ПараметрСозданияZIP = ПолучитьПараметрСозданияZIP(СписокФайлов, ПолныйПутьZipФайла);
		
		Если ВнешняяКомпонентаMobileUtils = Неопределено Тогда
			
			ВызватьИсключение НСтр("ru = 'Компонента MobileUtils не инициализирована'");
			
		Иначе
			
			Успешно = ВнешняяКомпонентаMobileUtils.СоздатьZIP(ПараметрСозданияZIP);
			
			Если НЕ Успешно Тогда
				
				ВызватьИсключение ВнешняяКомпонентаMobileUtils.GetLastError();
				
			КонецЕсли;
			
		КонецЕсли;
		
	#Иначе
		
		МобильныеУтилитыВызовСервера.СоздатьZIP(СписокФайлов, ПолныйПутьZipФайла);
		
	#КонецЕсли
	
КонецПроцедуры

Функция ПолучитьВерсиюКомпоненты() Экспорт
	
	ВнешняяКомпонентаMobileUtils = ПолучитьОбъектКомпоненты();
	
	Если НЕ ВнешняяКомпонентаMobileUtils = Неопределено Тогда
		
		Версия = ВнешняяКомпонентаMobileUtils.ПолучитьНомерВерсии();
		
		Возврат Версия;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметрСозданияZIP(СписокФайлов, ПолныйПутьZipФайла)
	
	ПараметрJSON = "";
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, " ", Истина);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("PathFileZip");
	ЗаписьJSON.ЗаписатьЗначение(ПолныйПутьZipФайла);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("FilePaths");
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Для Каждого ПутьФайла Из СписокФайлов Цикл
		ЗаписьJSON.ЗаписатьЗначение(ПутьФайла);
	КонецЦикла;
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ПараметрJSON = ЗаписьJSON.Закрыть();
	
	Возврат ПараметрJSON;
	
КонецФункции

Процедура ПослеПодключенияКомпоненты(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		
		Попытка
			
			ВнешняяКомпонентаMobileUtils = МобильныеУтилитыКлиентПовтИсп.ПолучитьВнешнююКомпоненту(
				ПолучитьИдентификаторТипаКомпоненты()
			);
			
		Исключение
			Сообщить("Ошибка создания класса MobileUtils");
		Конецпопытки;
		
	Иначе
		
		Сообщить("Ошибка подключения внешней компоненты");
		
	КонецЕсли
	
КонецПроцедуры

Функция ПолучитьОбъектКомпоненты()
	
	Попытка
		КомпонентаУтилит = МобильныеУтилитыКлиентПовтИсп.ПолучитьВнешнююКомпоненту(
			ПолучитьИдентификаторТипаКомпоненты()
		);
	Исключение
		
		КомпонентаУтилит = Неопределено;
	КонецПопытки;
	
	Возврат КомпонентаУтилит;
	
КонецФункции

Функция ПолучитьИдентификаторТипаКомпоненты()
	
	Возврат "AddIn.com_1c_MobileUtils.MobileUtils";
	
КонецФункции

#КонецОбласти
