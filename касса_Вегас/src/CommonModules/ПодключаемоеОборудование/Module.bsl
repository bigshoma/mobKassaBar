
#Область ПрограммныйИнтерфейс

Процедура ОпределитьПодключаемоеОборудование(Драйвер, ПодключаемоеОборудование) Экспорт
	
	ПодключаемоеОборудование = Справочники.ПодключаемоеОборудование.ПустаяСсылка();
	
	Если НЕ ЗначениеЗаполнено(Драйвер) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПодключаемоеОборудование.Ссылка
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	ПодключаемоеОборудование.ДрайверОборудования = &ДрайверОборудования";
	
	Запрос.УстановитьПараметр("ДрайверОборудования", Драйвер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ПодключаемоеОборудование = Выборка.Ссылка;
		
		Если НЕ ПодключаемоеОборудование.УстройствоИспользуется Тогда
			ПодключаемоеОборудованиеОбъект = ПодключаемоеОборудование.ПолучитьОбъект();
			ПодключаемоеОборудованиеОбъект.УстройствоИспользуется = Истина;
			ПодключаемоеОборудованиеОбъект.Записать();
		КонецЕсли;
		
	Иначе
		
		РабочееМесто = МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента();
		
		ПодключаемоеОборудованиеОбъект = Справочники.ПодключаемоеОборудование.СоздатьЭлемент();
		
		ПодключаемоеОборудованиеОбъект.ТипОборудования        = Драйвер.ТипОборудования;
		ПодключаемоеОборудованиеОбъект.ДрайверОборудования    = Драйвер;
		ПодключаемоеОборудованиеОбъект.Наименование           = Драйвер.Наименование;
		ПодключаемоеОборудованиеОбъект.УстройствоИспользуется = Истина;
		ПодключаемоеОборудованиеОбъект.РабочееМесто           = РабочееМесто;
		ПодключаемоеОборудованиеОбъект.Записать();
		
		ПодключаемоеОборудование = ПодключаемоеОборудованиеОбъект.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьТипОплатыККТ(ТипОплаты) Экспорт
	
	Если ТипОплаты = Перечисления.ТипыОплаты.Наличные Тогда
		
		Возврат Перечисления.ТипыОплатыККТ.Наличные;
		
	ИначеЕсли ТипОплаты = Перечисления.ТипыОплаты.ПлатежнаяКарта Тогда
		
		Возврат Перечисления.ТипыОплатыККТ.Электронно;
		
	ИначеЕсли ТипОплаты = Перечисления.ТипыОплаты.ВстречноеПредоставление Тогда
		
		Возврат Перечисления.ТипыОплатыККТ.ВстречноеПредоставление;
		
	ИначеЕсли ТипОплаты = Перечисления.ТипыОплаты.Постоплата Тогда
		
		Возврат Перечисления.ТипыОплатыККТ.Постоплата;
		
	ИначеЕсли ТипОплаты = Перечисления.ТипыОплаты.Предоплата Тогда
		
		Возврат Перечисления.ТипыОплатыККТ.Предоплата;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти
