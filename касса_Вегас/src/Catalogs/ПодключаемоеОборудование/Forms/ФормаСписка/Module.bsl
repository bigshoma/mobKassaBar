
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПользовательскийИнтерфейс();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДрайверыОборудования(Команда)
	
	ОткрытьФорму("Справочник.ДрайверыОборудования.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура Настроить(Команда)
	
	ОчиститьСообщения();
	
	Если Элементы.СписокУстройств.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерОборудованияКлиент.ВыполнитьНастройкуОборудования(Элементы.СписокУстройств.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРабочиеМеста(Команда)
	
	Элементы.СписокВсеРабочиеМеста.Пометка = НЕ Элементы.СписокВсеРабочиеМеста.Пометка; 
	Элементы.РабочееМесто.Видимость = Элементы.СписокВсеРабочиеМеста.Пометка; 
	
	СписокУстройств.Отбор.Элементы[0].Использование = (Не Элементы.СписокВсеРабочиеМеста.Пометка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПользовательскийИнтерфейс()
	
	Если ТекущееРабочееМесто = Справочники.РабочиеМеста.ПустаяСсылка()
		Или ТекущееРабочееМесто <> ПараметрыСеанса.РабочееМестоКлиента Тогда
		ТекущееРабочееМесто = ПараметрыСеанса.РабочееМестоКлиента;
	КонецЕсли;
		
	ЭлементОтбора = СписокУстройств.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РабочееМесто");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = ТекущееРабочееМесто;
	ЭлементОтбора.Использование = Истина;
	
#Если МобильноеПриложениеСервер Тогда
	ОтображатьДопЭлементы = Ложь;
#Иначе
	ОтображатьДопЭлементы = Истина;
#КонецЕсли
	
	Элементы.ДрайверыОборудования.Видимость = ОтображатьДопЭлементы;
	Элементы.СписокВсеРабочиеМеста.Видимость = ОтображатьДопЭлементы;
	Элементы.ФормаНастроить.Видимость = ОтображатьДопЭлементы;
	Элементы.СписокУстройств.Шапка = ОтображатьДопЭлементы;
	Элементы.ДрайверОборудования.Видимость = ОтображатьДопЭлементы;
	
КонецПроцедуры

#КонецОбласти


