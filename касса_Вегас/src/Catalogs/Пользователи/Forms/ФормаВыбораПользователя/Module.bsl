
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("АвторизованныйПользователь")
		И ЗначениеЗаполнено(Параметры.АвторизованныйПользователь) Тогда
		
		АвторизованныйПользователь = Параметры.АвторизованныйПользователь;
	КонецЕсли;
	
	ОбщегоНазначения.УстановитьЦветИнформационныхНадписей(ЭтотОбъект, "НадписьЗаголовок");
	
	НастроитьАвторизованногоПользователя();
	
	НастроитьФормуПоЗначениямНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыЗначенияНастроек" Тогда
		НастроитьФормуПоЗначениямНастроек();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ПлановоеЗакрытиеФормы Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(АвторизованныйПользователь) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выйти(Команда)
	
	Оповестить("ВыходПользователя");
	
	ПомощникUIКлиент.ПоказатьСообщение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 вышел из системы'"),
		ФИО));
	
	Элементы.Выйти.Видимость = Ложь;
	Элементы.ЗакрытьФорму.Видимость = Ложь;
	
	АвторизованныйПользователь = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРаботу(Команда)
	
	ПлановоеЗакрытиеФормы = Истина;
	Закрыть("ЗавершитьРаботуСистемы");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПлановоеЗакрытиеФормы = Истина;
	
	Закрыть(Элементы.Список.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьАвторизованногоПользователя()
	
	Если ЗначениеЗаполнено(АвторизованныйПользователь) Тогда
		
		ФИО = ОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(АвторизованныйПользователь, "Наименование");
		
		Элементы.Выйти.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Выйти (%1)'"),
			ФИО);
			
		Элементы.ЗакрытьФорму.Видимость = Истина;
		
		Элементы.СтраницыПодвал.ТекущаяСтраница = Элементы.СтраницаВыйти;
		
	Иначе
		Элементы.СтраницыПодвал.ТекущаяСтраница = Элементы.СтраницаЗавершитьРаботу;
		Элементы.ЗакрытьФорму.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти





