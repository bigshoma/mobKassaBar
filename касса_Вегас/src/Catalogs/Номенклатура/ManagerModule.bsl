
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция КоличествоЭлементовИГрупп() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Номенклатура.Ссылка) КАК КоличествоПозиций
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КоличествоПозиций;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция НеобходимоВводитьАкцизныеМарки(Номенклатура, Дата = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыНоменклатуры = ОбщегоНазначенияВызовСервера.ЗначенияРеквизитовОбъекта(
		Номенклатура, "АлкогольнаяПродукция, МаркируемаяАлкогольнаяПродукция"
	);
	
	Если РеквизитыНоменклатуры.АлкогольнаяПродукция И РеквизитыНоменклатуры.МаркируемаяАлкогольнаяПродукция
		И ЗначениеНастроекВызовСервераПовтИсп.ИспользуетсяЕГАИСПриПродаже(Дата) Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция НеобходимоВводитьМарку(Номенклатура) Экспорт
	
	РеквизитыНоменклатуры = ОбщегоНазначенияВызовСервера.ЗначенияРеквизитовОбъекта(
		Номенклатура, "ТипМаркировки, АлкогольнаяПродукция"
	);
	
	Если ЗначениеЗаполнено(РеквизитыНоменклатуры.ТипМаркировки) И НЕ РеквизитыНоменклатуры.АлкогольнаяПродукция Тогда
		 Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

Функция ОпределитьНеобходимостьШтрихкодаПриПродаже(Номенклатура, Дата = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеНастроекВызовСервераПовтИсп.ИспользуетсяЕГАИСПриПродаже(Дата) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыНоменклатуры = ОбщегоНазначенияВызовСервера.ЗначенияРеквизитовОбъекта(
		Номенклатура, "АлкогольнаяПродукция, МаркируемаяАлкогольнаяПродукция"
	);
	
	Если РеквизитыНоменклатуры.АлкогольнаяПродукция
		И РеквизитыНоменклатуры.МаркируемаяАлкогольнаяПродукция Тогда
		
		Возврат Истина;
	КонецЕсли;
		
	Если РеквизитыНоменклатуры.АлкогольнаяПродукция
		И ЗначениеНастроекВызовСервераПовтИсп.ВыгружатьПродажиНемаркируемойПродукцииВЕГАИС() Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПодобратьШтрихкодДляПродажи (Номенклатура) Экспорт
	
	Штрихкод = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Штрихкоды.Штрихкод КАК Штрихкод
		|ИЗ
		|	РегистрСведений.Штрихкоды КАК Штрихкоды
		|ГДЕ
		|	Штрихкоды.Номенклатура = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Штрихкод = Выборка.Штрихкод;
	КонецЕсли;
	
	Возврат Штрихкод;
	
КонецФункции

#КонецОбласти

#КонецЕсли