

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	НаборКонстант = Константы.СоздатьНабор(СписокКонстант());
	НаборКонстант.Прочитать();
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НаборКонстант, СписокКонстант());
	
	// ОриентацияЭкрана
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьПараметровГруппПодбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеКлиент Тогда
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	ОбщегоНазначенияКлиент.ПередЗакрытиемФормыНастроек(ЭтаФорма, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ОтображатьГруппыВПодбореТоваровПриИзменении(Элемент)
	
	УстановитьДоступностьПараметровГруппПодбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьНаКлиенте() Экспорт
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	Если НЕ ПроверитьЗаполнениеФормы() Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		СохранитьНаСервере(Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		Если ОповеститьОбИзмененииЗначенийНастроек Тогда
			Оповестить("ИзмененыЗначенияНастроек");
		КонецЕсли;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере(Отказ)
	
	ОбщегоНазначения.СохранитьКонстантыФормы(ЭтаФорма, СписокКонстант(), Отказ);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	
	ОбщегоНазначения.НастроитьКомандуГотово(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьПараметровГруппПодбора()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "КоличествоРядовГруппВПодборе, КоличествоГруппВОдномРядуПодбора", "Доступность",
		ОтображатьГруппыВПодбореТоваров);
	
КонецПроцедуры

&НаСервере
Функция СписокКонстант()
	
	СписокКонстант =
	"РазмерШрифта,
	|ТочностьОтображенияКоличества,
	|ОтображатьГруппыВПодбореТоваров,
	|КоличествоРядовГруппВПодборе,
	|КоличествоГруппВОдномРядуПодбора,
	|ВысотаПоляНаименованиеТовара,
	|ОткрыватьПодборПослеПробитияЧека";
	
	Возврат СписокКонстант;
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеФормы()
	
	Если ОтображатьГруппыВПодбореТоваров Тогда
		
		Если НЕ ЗначениеЗаполнено(КоличествоРядовГруппВПодборе) Тогда
			
			ПомощникUIКлиент.СообщитьПолеНеЗаполнено(ЭтотОбъект,
				Элементы.КоличествоРядовГруппВПодборе,
				НСтр("ru = 'Количество рядов'")
			);
			
			Возврат Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(КоличествоГруппВОдномРядуПодбора) Тогда
			
			ПомощникUIКлиент.СообщитьПолеНеЗаполнено(ЭтотОбъект,
				Элементы.КоличествоГруппВОдномРядуПодбора,
				НСтр("ru = 'Кол-во групп в одном ряду'")
			);
			
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ОриентацияЭкрана

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
КонецПроцедуры

#КонецОбласти
