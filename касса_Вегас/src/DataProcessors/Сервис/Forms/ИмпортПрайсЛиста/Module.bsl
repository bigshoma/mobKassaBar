
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ИмпортПрайсЛистаПутьКФайлу,    ПутьКФайлуИмпорта);
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ИмпортПрайсЛистаСтавкаНДС,     СтавкаНДСПоУмолчанию);
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ИмпортПрайсЛистаЕдИзм,         ЕдиницаИзмеренияПоУмолчанию);
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ИмпортПрайсЛистаВариантПоиска, ВариантПоиска);
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ИмпортПрайсЛистаГруппа,        ГруппаПоУмолчанию);
	
	ЗаполнитьСписокСтавокНДС();
	
	Если НЕ ЗначениеЗаполнено(СтавкаНДСПоУмолчанию) Тогда
		
		СтавкаНДСПоУмолчанию = ЦенообразованиеКлиентСервер.ПолучитьСтавку_18_20(Перечисления.СтавкиНДС.НДС18);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияПоУмолчанию) Тогда
		ЕдиницаИзмеренияПоУмолчанию = ОбщегоНазначенияПовтИсп.ПолучитьЕдиницуИзмерения("шт");
	КонецЕсли;
	
	ПредметРасчетаПоУмолчанию = Перечисления.ПризнакиПредметаРасчета.Товар;
	
	НастроитьФормуПоЗначениямНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПутьКФайлуИмпортаПоУмолчанию = КаталогДокументов() + "import.xml";
	
	Если НЕ ЗначениеЗаполнено(ПутьКФайлуИмпорта) Тогда
		ПутьКФайлуИмпорта = ПутьКФайлуИмпортаПоУмолчанию;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеКлиент Тогда
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	ПередЗакрытиемСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПутьКФайлу(Команда)
	
	ПутьКФайлуИмпорта = ПутьКФайлуИмпортаПоУмолчанию;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлИмпорта(Команда)
	
	ОчиститьСообщения();
	
	ЕстьОшибки = Ложь;
	
	Если ПроверитьЗаполнениеФормы() Тогда
		
		Файл = Новый Файл(ПутьКФайлуИмпорта);
		Если НЕ Файл.Существует() Тогда
			СообщениеОбОшибке = НСтр("ru = 'Указанный файл не обнаружен'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
			ЕстьОшибки = Истина;
			Возврат;
		КонецЕсли;
	Иначе
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияВызовСервера.ОбновитьПовторноИспользуемыеЗначенияСервер();
	
	Если Не ЕстьОшибки Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПослеЗагрузкиПрайсЛиста", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПутьКФайлу", ПутьКФайлуИмпорта);
		ПараметрыФормы.Вставить("ВариантПоиска", ВариантПоиска);
		ПараметрыФормы.Вставить("СтавкаНДСПоУмолчанию", СтавкаНДСПоУмолчанию);
		ПараметрыФормы.Вставить("ЕдиницаИзмеренияПоУмолчанию", ЕдиницаИзмеренияПоУмолчанию);
		ПараметрыФормы.Вставить("ГруппаПоУмолчанию", ГруппаПоУмолчанию);
		ПараметрыФормы.Вставить("ПредметРасчетаПоУмолчанию", ПредметРасчетаПоУмолчанию);
		
		ОткрытьФорму("Обработка.Сервис.Форма.ИмпортПрайсЛистаДанныеЗагрузки", ПараметрыФормы, ЭтаФорма,,,,ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверитьЗаполнениеФормы()
	
	Если НЕ ЗначениеЗаполнено(ПутьКФайлуИмпорта) Тогда
		
		ПомощникUIКлиент.СообщитьПолеНеЗаполнено(ЭтотОбъект, Элементы.ПутьКФайлуИмпорта, НСтр("ru = 'Путь к файлу'"));
		
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтавкаНДСПоУмолчанию) Тогда
		
		ПомощникUIКлиент.СообщитьПолеНеЗаполнено(ЭтотОбъект, Элементы.СтавкаНДСПоУмолчанию, НСтр("ru = 'Ставка НДС'"));
		
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмеренияПоУмолчанию) Тогда
		
		ПомощникUIКлиент.СообщитьПолеНеЗаполнено(ЭтотОбъект, Элементы.ЕдиницаИзмеренияПоУмолчанию, НСтр("ru = 'Единица измерения'"));
		
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПредметРасчетаПоУмолчанию) Тогда
		
		ПомощникUIКлиент.СообщитьПолеНеЗаполнено(ЭтотОбъект, Элементы.ПредметРасчетаПоУмолчанию, НСтр("ru = 'Предмет расчета'"));
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ПередЗакрытиемСервер()
	
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ИмпортПрайсЛистаПутьКФайлу",    ПутьКФайлуИмпорта);
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ИмпортПрайсЛистаСтавкаНДС",     СтавкаНДСПоУмолчанию);
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ИмпортПрайсЛистаЕдИзм",         ЕдиницаИзмеренияПоУмолчанию);
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ИмпортПрайсЛистаВариантПоиска", ВариантПоиска);
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ИмпортПрайсЛистаГруппа",        ГруппаПоУмолчанию);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПослеЗагрузкиПрайсЛиста(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		
		ТекстСообщения = НСтр("ru = 'Импорт прайс-листа успешно завершен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСтавокНДС()
	
	Элементы.СтавкаНДСПоУмолчанию.СписокВыбора.Очистить();
	
	Если ЦенообразованиеКлиентСервер.ПрименяетсяСтавкаНДС20() Тогда
		Элементы.СтавкаНДСПоУмолчанию.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС20, НСтр("ru = '20%'"));
	Иначе
		Элементы.СтавкаНДСПоУмолчанию.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС18, НСтр("ru = '18%'"));
	КонецЕсли;
	
	Элементы.СтавкаНДСПоУмолчанию.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС10, НСтр("ru = '10%'"));
	Элементы.СтавкаНДСПоУмолчанию.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС0,  НСтр("ru = '0%'"));
	Элементы.СтавкаНДСПоУмолчанию.СписокВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС, НСтр("ru = 'Без НДС'"));
	
КонецПроцедуры

#КонецОбласти

