
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	// ОриентацияЭкрана
	НастроитьФормуПоОриентацииЭкрана();
	
	КассоваяСмена = Продажи.ПолучитьКассовуюСменуДляОтчета();
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПоEmail(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		
		СтруктураПисьма = ПолучитьСтруктуруПисьма();
		
		РаботаСПочтойКлиент.ОтправитьОтчет(СтруктураПисьма);
		
	#Иначе
		
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Операция доступна только из мобильного приложения'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Результат.Напечатать(РежимИспользованияДиалогаПечати.Использовать);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьСтруктуруПисьма()
	
	СтруктураПисьма = РаботаСПочтойКлиент.СтруктураПисьмаОтчета();
	
	СтруктураПисьма.ТемаПисьма  = ПолучитьТемуПисьма();
	СтруктураПисьма.ТекстПисьма = ОбщегоНазначенияКлиентСервер.ПолучитьТекстДатаФормированияОтчета(ДатаФормирования);
	
	ЗаписанныйФайл = РаботаСПочтойКлиент.ЗаписатьТабДокВФайлПисьма(Результат, СтруктураПисьма.ТемаПисьма);
	
	РаботаСПочтойКлиент.ДобавитьЗаписанныйФайлВоВложениеПисьма(СтруктураПисьма, ЗаписанныйФайл);
	
	Возврат СтруктураПисьма;
	
КонецФункции

&НаСервере
Процедура СформироватьНаСервере()
	
	Результат.Очистить();
	
	Если НЕ ЗначениеЗаполнено(КассоваяСмена) Тогда
		Возврат;
	КонецЕсли;
	
	МакетОтчета = Обработки.Отчеты.ПолучитьМакет("ОтчетПоКассовойСмене");
	ДанныеОтчета = Продажи.ОтчетПоКассе(КассоваяСмена);
	ЗаполнитьЗначенияСвойств(МакетОтчета.Параметры, ДанныеОтчета);
	
	МакетОтчета.Параметры.Номер  = ПродажиВызовСервера.ПолучитьНомерСменыДляПечати(КассоваяСмена);
	
	МакетОтчета.Параметры.Дата   = Формат(КассоваяСмена.Дата, "ДФ=dd.MM.yyyy; ДЛФ=D");
	МакетОтчета.Параметры.НачалоСмены = КассоваяСмена.НачалоКассовойСмены;
	МакетОтчета.Параметры.СтатусСмены = ?(КассоваяСмена.Закрыта, "Закрыта", "Открыта");
	МакетОтчета.Параметры.ОкончаниеСмены = Формат(КассоваяСмена.ОкончаниеКассовойСмены, "ДП=-");
	МакетОтчета.Параметры.ОкончаниеСмены = Формат(КассоваяСмена.ОкончаниеКассовойСмены, "ДП=-");
	
	Если ЗначениеЗаполнено(КассоваяСмена.Кассир) Тогда
		МакетОтчета.Параметры.Кассир = НСтр("ru = 'Кассир:'") + " " + КассоваяСмена.Кассир;
	КонецЕсли;
	
	Результат.Вывести(МакетОтчета);
	
	ДатаФормирования = ОбщегоНазначенияКлиентСервер.ПолучитьТекущуюДату();
	
КонецПроцедуры

&НаКлиенте
Процедура КассоваяСменаПриИзменении(Элемент)
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТемуПисьма()
	
	ШаблонТемыПисьма = НСтр("ru = '[1С:МК] Отчет по кассовой смене %НомерСмены% от %ДатаСмены%'");
	ТемаПисьма = СтрЗаменить(ШаблонТемыПисьма, "%НомерСмены%", ПродажиВызовСервера.ПолучитьНомерСменыДляПечати(КассоваяСмена));
	ТемаПисьма = СтрЗаменить(ТемаПисьма, "%ДатаСмены%", Формат(КассоваяСмена.Дата, "ДФ=dd.MM.yyyy; ДЛФ=D"));
	
	Возврат ТемаПисьма;
	
КонецФункции

#КонецОбласти

#Область ОриентацияЭкрана 

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
КонецПроцедуры

#КонецОбласти
