
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	// ОриентацияЭкрана
	НастроитьФормуПоОриентацииЭкрана();
	
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ОтчетПродажиВариант, ВариантОтчета);
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ОтчетПродажиПериод, Период);
	ОбщегоНазначения.ВосстановитьНастройкуПользователя(Перечисления.НастройкиПользователя.ОтчетПродажиПоГоризонтали, ПоВертикалиГоризонтали);
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Период.Вариант = ВариантСтандартногоПериода.Сегодня;
	КонецЕсли;
	
	Кассир = ПараметрыСеанса.ТекущийПользователь;
	КассоваяСмена = Продажи.ПолучитьКассовуюСменуДляОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеКлиент Тогда
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ОтчетПродажиВариант", ВариантОтчета);
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ОтчетПродажиПериод", Период);
	ОбщегоНазначенияВызовСервера.СохранитьНастройкуПользователя("ОтчетПродажиПоГоризонтали", ПоВертикалиГоризонтали);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьКнопкиПоВертикалиГоризонтали();
	
	ПриСменеВарианта();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ОповещениеВыборПериода", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура("Период, СкрытьОтменить", Период, Истина);
	ОткрытьФорму("ОбщаяФорма.НастройкаПериода", ПараметрыФормы,,,,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура КассирПриИзменении(Элемент)
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КассоваяСменаПриИзменении(Элемент)
	
	СформироватьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПоEmail(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		
		СтруктураПисьма = ПолучитьСтруктуруПисьма();
		
		РаботаСПочтойКлиент.ОтправитьОтчет(СтруктураПисьма);
		
	#Иначе
		
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Операция доступна только из мобильного приложения'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Результат.Напечатать(РежимИспользованияДиалогаПечати.Использовать);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоВертикали(Команда)
	
	ПоВертикалиГоризонтали = 0;
	НастроитьКнопкиПоВертикалиГоризонтали();
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоГоризонтали(Команда)
	
	ПоВертикалиГоризонтали = 1;
	НастроитьКнопкиПоВертикалиГоризонтали();
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаКассовуюСмену(Команда)
	
	ВариантОтчета = ВариантЗаКассовуюСмену();
	
	ПриСменеВарианта();
КонецПроцедуры

&НаКлиенте
Процедура ЗаПериод(Команда)
	
	ВариантОтчета = ВариантЗаПериод();
	
	ПриСменеВарианта();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриСменеВарианта()
	
	НастроитьКнопкиВариантовОтчета();
	
	УстановитьСтраницуОтбора();
	СформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуОтбора()
	
	Если ВариантОтчета = ВариантЗаКассовуюСмену() Тогда
		
		Элементы.СтраницаЗаКассовуюСмену.Видимость = Истина;
		Элементы.СтраницыОтборыПоВарианту.ТекущаяСтраница = Элементы.СтраницаЗаКассовуюСмену;
		Элементы.СтраницаЗаПериод.Видимость = Ложь;
		
	Иначе
		
		Элементы.СтраницаЗаПериод.Видимость = Истина;
		Элементы.СтраницыОтборыПоВарианту.ТекущаяСтраница = Элементы.СтраницаЗаПериод;
		Элементы.СтраницаЗаКассовуюСмену.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьКнопкиПоВертикалиГоризонтали()
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		
		
		ОбщегоНазначенияКлиентСервер.УстановитьПометкуКнопки(Элементы.ПоВертикали);
		ОбщегоНазначенияКлиентСервер.СнятьПометкуКнопки(Элементы.ПоГоризонтали);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьПометкуКнопки(Элементы.ПоГоризонтали);
		ОбщегоНазначенияКлиентСервер.СнятьПометкуКнопки(Элементы.ПоВертикали);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьКнопкиВариантовОтчета()
	
	Если ВариантОтчета = ВариантЗаКассовуюСмену() Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьПометкуКнопки(Элементы.ЗаКассовуюСмену);
		ОбщегоНазначенияКлиентСервер.СнятьПометкуКнопки(Элементы.ЗаПериод);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьПометкуКнопки(Элементы.ЗаПериод);
		ОбщегоНазначенияКлиентСервер.СнятьПометкуКнопки(Элементы.ЗаКассовуюСмену);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруПисьма()
	
	СтруктураПисьма = РаботаСПочтойКлиент.СтруктураПисьмаОтчета();
	
	СтруктураПисьма.ТемаПисьма  = ПолучитьТемуПисьма();
	СтруктураПисьма.ТекстПисьма = ОбщегоНазначенияКлиентСервер.ПолучитьТекстДатаФормированияОтчета(ДатаФормирования);
	
	ЗаписанныйФайл = РаботаСПочтойКлиент.ЗаписатьТабДокВФайлПисьма(Результат, СтруктураПисьма.ТемаПисьма);
	
	РаботаСПочтойКлиент.ДобавитьЗаписанныйФайлВоВложениеПисьма(СтруктураПисьма, ЗаписанныйФайл);
	
	Возврат СтруктураПисьма;
	
КонецФункции

&НаСервере
Процедура СформироватьНаСервере()
	
	Результат.Очистить();
	
	Если ВариантОтчета = ВариантЗаПериод() Тогда
		
		ДанныеОтчета = Продажи.ПолучитьОтчетПоПродажам(, Период, Кассир);
		
	ИначеЕсли ВариантОтчета = ВариантЗаКассовуюСмену() Тогда
		
		Если Не ЗначениеЗаполнено(КассоваяСмена) Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеОтчета = Продажи.ПолучитьОтчетПоПродажам(КассоваяСмена);
		
	КонецЕсли;
	
	МакетОтчета = Обработки.Отчеты.ПолучитьМакет("ОтчетПоПродажам");
	
	ПустаяСтрока = МакетОтчета.ПолучитьОбласть("ПустаяСтрока");
	Результат.Вывести(ПустаяСтрока);
	
	#Область ЗаголовокОтчета
	
	Если ВариантОтчета = ВариантЗаПериод() Тогда
		
		Если ПоВертикалиГоризонтали = 0 Тогда
			ЗаголовокОтчета = МакетОтчета.ПолучитьОбласть("ЗаголовокПериодВертикальный");
		Иначе
			ЗаголовокОтчета = МакетОтчета.ПолучитьОбласть("ЗаголовокПериод");
		КонецЕсли;
		
		ЗаголовокОтчета.Параметры.ДатаС  = Формат(Период.ДатаНачала,    "ДЛФ=D");
		ЗаголовокОтчета.Параметры.ДатаПо = Формат(Период.ДатаОкончания, "ДЛФ=D");
		
	Иначе // за кассовую смену
		
		Если ПоВертикалиГоризонтали = 0 Тогда
			ЗаголовокОтчета = МакетОтчета.ПолучитьОбласть("ЗаголовокКассоваяСменаВертикальный");
		Иначе
			ЗаголовокОтчета = МакетОтчета.ПолучитьОбласть("ЗаголовокКассоваяСмена");
		КонецЕсли;
		
		ЗаголовокОтчета.Параметры.Номер = ПродажиВызовСервера.ПолучитьНомерСменыДляПечати(КассоваяСмена);
		
		ЗаголовокОтчета.Параметры.Дата   = Формат(КассоваяСмена.Дата, "ДФ=dd.MM.yyyy; ДЛФ=D");
		
	КонецЕсли;
	
	Результат.Вывести(ЗаголовокОтчета);
	Результат.Вывести(ПустаяСтрока);
	
	#КонецОбласти //ЗаголовокОтчета

	#Область ЗаголовокТаблицыПродажи
	
	ЗаголовокТаблицыПродажи = МакетОтчета.ПолучитьОбласть("ЗаголовокПродажи");
	
	Результат.Вывести(ЗаголовокТаблицыПродажи);
	
	#КонецОбласти //ЗаголовокТаблицыПродажи

	#Область ШапкаТаблицыПродажи
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		Шапка = МакетОтчета.ПолучитьОбласть("ШапкаВертикальная");
	Иначе
		Шапка = МакетОтчета.ПолучитьОбласть("Шапка");
	КонецЕсли;
	
	Результат.Вывести(Шапка);
	
	#КонецОбласти //ШапкаТаблицыПродажи

	#Область СтрокиТаблицыПродажи
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		Строка = МакетОтчета.ПолучитьОбласть("СтрокаВертикальная");
		СтрокаЦвет = МакетОтчета.ПолучитьОбласть("СтрокаВертикальнаяЦвет");
	Иначе
		Строка = МакетОтчета.ПолучитьОбласть("Строка");
		СтрокаЦвет = МакетОтчета.ПолучитьОбласть("СтрокаЦвет");
	КонецЕсли;
	
	НомерСтроки = 1;
	
	ТаблицаПродажи = ДанныеОтчета.ТаблицаПродажи;
	
	Для Каждого СтрокаОтчета Из ТаблицаПродажи Цикл
		
		Если НомерСтроки % 2 = 0 Тогда
			СтрокаМакета = СтрокаЦвет;
		Иначе
			СтрокаМакета = Строка;
		КонецЕсли;
		
		СтрокаМакета.Параметры.Заполнить(СтрокаОтчета);
		СтрокаМакета.Параметры.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		
		Результат.Вывести(СтрокаМакета);
	КонецЦикла;
	
	#КонецОбласти //СтрокиТаблицыПродажи

	#Область ИтогоТаблицыПродажи
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		Итого = МакетОтчета.ПолучитьОбласть("ИтогоПродажаВертикальное");
	Иначе
		Итого = МакетОтчета.ПолучитьОбласть("ИтогоПродажа");
	КонецЕсли;
	
	Итого.Параметры.СуммаИтого = ТаблицаПродажи.Итог("Сумма");
	Итого.Параметры.СуммаИтогоОплата = ДанныеОтчета.СуммаОплатыПродажа;

	Результат.Вывести(Итого);
	
	#КонецОбласти //ИтогоТаблицыПродажи

	// Возвраты товаров
	Результат.Вывести(ПустаяСтрока);

	#Область ЗаголовокТаблицыВозврата
	
	ЗаголовокТаблицыВозвтрата = МакетОтчета.ПолучитьОбласть("ЗаголовокВозвраты");
	
	Результат.Вывести(ЗаголовокТаблицыВозвтрата);
	//Результат.Вывести(ПустаяСтрока);
	
	#КонецОбласти //ЗаголовокТаблицыВозврата

	#Область ШапкаТаблицыВозврата
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		Шапка = МакетОтчета.ПолучитьОбласть("ШапкаВертикальная");
	Иначе
		Шапка = МакетОтчета.ПолучитьОбласть("Шапка");
	КонецЕсли;
	Результат.Вывести(Шапка);
	
	#КонецОбласти //ШапкаТаблицыВозврата

	#Область СтрокиТаблицыВозврата
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		Строка = МакетОтчета.ПолучитьОбласть("СтрокаВертикальная");
		СтрокаЦвет = МакетОтчета.ПолучитьОбласть("СтрокаВертикальнаяЦвет");
	Иначе
		Строка = МакетОтчета.ПолучитьОбласть("Строка");
		СтрокаЦвет = МакетОтчета.ПолучитьОбласть("СтрокаЦвет");
	КонецЕсли;
	
	НомерСтроки = 1;
	
	ТаблицаВозвраты = ДанныеОтчета.ТаблицаВозвраты;
	
	Для Каждого СтрокаОтчета Из ТаблицаВозвраты Цикл
		
		Если НомерСтроки % 2 = 0 Тогда
			СтрокаМакета = СтрокаЦвет;
		Иначе
			СтрокаМакета = Строка;
		КонецЕсли;
		
		СтрокаМакета.Параметры.Заполнить(СтрокаОтчета);
		СтрокаМакета.Параметры.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		
		Результат.Вывести(СтрокаМакета);
	КонецЦикла;
	
	#КонецОбласти //СтрокиТаблицыВозврата

	#Область ИтогоТаблицыВозврата
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		Итого = МакетОтчета.ПолучитьОбласть("ИтогоВозвратВертикальное");
	Иначе
		Итого = МакетОтчета.ПолучитьОбласть("ИтогоВозврат");
	КонецЕсли;
	
	Итого.Параметры.СуммаИтого = ТаблицаВозвраты.Итог("Сумма");
	Итого.Параметры.СуммаИтогоОплата = ДанныеОтчета.СуммаОплатыВозврат;
	
	Результат.Вывести(Итого);
	
	#КонецОбласти //ИтогоТаблицыВозврата
	
	Результат.Вывести(ПустаяСтрока);
	
	#Область ИтогоВсего
	
	Если ПоВертикалиГоризонтали = 0 Тогда
		Итого = МакетОтчета.ПолучитьОбласть("ИтогоВсегоВертикальное");
	Иначе
		Итого = МакетОтчета.ПолучитьОбласть("ИтогоВсего");
	КонецЕсли;
	
	Итого.Параметры.СуммаИтого = ТаблицаПродажи.Итог("Сумма") - ТаблицаВозвраты.Итог("Сумма");
	Итого.Параметры.СуммаИтогоОплата = ДанныеОтчета.СуммаОплатыИтого;
	
	Результат.Вывести(Итого);
	
	#КонецОбласти //ИтогоВсего
	
	#Область Кассир
	
	КассирВОтчет = Неопределено;
	
	Если ВариантОтчета = ВариантЗаПериод() Тогда
		КассирВОтчет = Кассир;
	Иначе
		КассирВОтчет = КассоваяСмена.Кассир;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КассирВОтчет) Тогда
		
		ПодвалКассир = МакетОтчета.ПолучитьОбласть("ПодвалКассир");
		ПодвалКассир.Параметры.Кассир = КассирВОтчет;
		Результат.Вывести(ПодвалКассир);
	КонецЕсли;
	
	#КонецОбласти //Кассир
	
	ДатаФормирования = ОбщегоНазначенияКлиентСервер.ПолучитьТекущуюДату();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыборПериода(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(Результат) = Тип("СтандартныйПериод") Тогда
		Возврат;
	КонецЕсли;
	
	Период = Результат;
	СформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТемуПисьма()
	
	ШаблонТемыПисьма = НСтр("ru = '[1С:МК] Продажи с %ДатаС% по %ДатаПо%'");
	
	ТемаПисьма = СтрЗаменить(ШаблонТемыПисьма, "%ДатаС%", Формат(Период.ДатаНачала, "ДЛФ=D"));
	ТемаПисьма = СтрЗаменить(ТемаПисьма, "%ДатаПо%", Формат(Период.ДатаОкончания, "ДЛФ=D"));
	
	Возврат ТемаПисьма;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантЗаКассовуюСмену()
	
	ВариантЗаКассовуюСмену = 0;
	
	Возврат ВариантЗаКассовуюСмену;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантЗаПериод()
	
	ВариантЗаПериод = 1;
	
	Возврат ВариантЗаПериод;
КонецФункции

#КонецОбласти

#Область ОриентацияЭкрана

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
КонецПроцедуры

#КонецОбласти


