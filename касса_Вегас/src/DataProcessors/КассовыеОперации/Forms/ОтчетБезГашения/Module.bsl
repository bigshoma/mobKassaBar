
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	НастроитьПоляФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаблокироватьФорму();
	
	ОтобразитьСтраницуОтчетОТекущемСостоянииРасчетов();
	ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОтчет", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеКлиент Тогда
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	Если НЕ ДоступноЗакрытиеФормы Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЕщеРаз(Команда)
	
	ЗаблокироватьФорму();
	
	ОтобразитьСтраницуОтчетОТекущемСостоянииРасчетов();
	ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОтчет", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ВыполнитьОтчет()
	
	ЕстьОшибки = Ложь;
	
	ВходныеПараметры = Новый Структура;
	
	Если ЗначениеНастроекВызовСервераПовтИсп.ПечататьИнформациюПоСменеПриСнятииКассовыхОтчетов() Тогда
		
		КассоваяСмена = ПродажиВызовСервераПовтИсп.ПолучитьСмену(Ложь);
		
		Если Не КассоваяСмена = Неопределено Тогда 
			
			ТекстОтчета = ПродажиВызовСервера.СформироватьТекстXОтчетаДляПечати(КассоваяСмена);
			ВходныеПараметры.Вставить("ТекстОтчета", ТекстОтчета);
			
			НомерСмены = ПродажиВызовСервера.ПолучитьНомерСменыДляПечати(КассоваяСмена, Ложь);
			ВходныеПараметры.Вставить("НомерСмены", НомерСмены);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПродажиКлиент.ПредзаполнитьПостоянныеПараметрыЧека(ВходныеПараметры);
	
	ВыходныеПараметры = Неопределено;
	
	ЛогированиеКлиентСервер.ЗаписатьСобытие("ОтчетаБезГашения");
	
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьФормированиеОтчетаБезГашения(УникальныйИдентификатор,
			ЗначениеНастроекВызовСервераПовтИсп.ОборудованиеПечати(), ВходныеПараметры, ВыходныеПараметры) Тогда
			
			ОтобразитьОшибку(ВыходныеПараметры.ТекстОшибки);
			ЕстьОшибки = Истина;
			
			ЛогированиеКлиентСервер.ЗаписатьСобытие("ОтчетаБезГашения", "Ошибка - " + ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;
	
	Если НЕ ЕстьОшибки Тогда
		
		ОтобразитьСтраницуУспешно();
		РазблокироватьФорму();
		ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьФорму", 1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

#Область ОтображениеСтраниц

&НаКлиенте
Процедура ОтобразитьОшибку(ОписаниеОшибки)
	
	ТекстОшибки = ОписаниеОшибки;
	
	РазблокироватьФорму();
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаОшибка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСтраницуОтчетОТекущемСостоянииРасчетов()
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаПечатьОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСтраницуУспешно()
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаУспешно;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтаФорма);
	
	ОборудованиеПечати = ЗначениеНастроекВызовСервераПовтИсп.ОборудованиеПечати();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоляФормы()
	
	ОбщегоНазначения.УстановитьЦветПолейОшибок(ЭтаФорма,
		"НадписьОшибка"
	);
	
	ОбщегоНазначения.УстановитьЦветПолейУспешныхОпераций(ЭтаФорма,
		"НадписьУспешно"
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаблокироватьФорму()
	
	ДоступноЗакрытиеФормы = Ложь;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "Закрыть", "Доступность",
		Ложь);

КонецПроцедуры

&НаКлиенте
Процедура РазблокироватьФорму()
	
	ДоступноЗакрытиеФормы = Истина;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, "Закрыть", "Доступность",
		Истина);

КонецПроцедуры

#КонецОбласти

#Область ОриентацияЭкрана

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
КонецПроцедуры

#КонецОбласти

