
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОборудованиеСканирования = ЗначениеНастроекВызовСервераПовтИсп.ОборудованиеСканированияШтрихкодов();
	
	НастроитьОформлениеПолей();
	
	НастроитьФормуПоЗначениямНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПодключаемоеОборудованиеВызовСервера.СканерШтрихкодовИнициализирован() Тогда
		ПодключаемоеОборудованиеКлиент.ОтключитьОборудование(ОборудованиеСканирования);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПодключитьСканер", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АктивироватьЕщеРаз(Команда)
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаПодключение;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПодключитьСканер", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ПодключитьСканер()
	
	РезультатПодключения = ПодключаемоеОборудованиеКлиент.ПодключитьОборудование(ОборудованиеСканирования);
	
	Если РезультатПодключения.Успешно Тогда
		
		ПодключаемоеОборудованиеВызовСервера.УстановитьСканерШтрихкодовИнициализирован(Истина);
		
		СканерПодключен = Истина;
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСканерПодключен;
		
		ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьФорму", 2, Истина);
		
	Иначе
		
		ПодключаемоеОборудованиеВызовСервера.УстановитьСканерШтрихкодовИнициализирован(Ложь);
		
		Лог = "";
		ОбщегоНазначенияКлиентСервер.ДополнитьТекст(Лог, РезультатПодключения.ОписаниеОшибки);
		
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаОшибка;
		
	КонецЕсли;
	
	Оповестить("ИнициализированАсинхронныйСканер");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	Закрыть(СканерПодключен);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОформлениеПолей()
	
	ОбщегоНазначения.УстановитьЦветПолейОшибок(ЭтаФорма, "ДекорацияОшибка");
	
	ОбщегоНазначения.УстановитьЦветПолейУспешныхОпераций(ЭтаФорма, "ДекорацияСканерПодключен");
	
КонецПроцедуры

#КонецОбласти
