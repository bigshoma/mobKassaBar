
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	ВосстановитьНастройкиПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеКлиент Тогда
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	СохранитьНастройкиПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыЗначенияНастроек" Тогда
		
		НастроитьФормуПоЗначениямНастроек();
		
	ИначеЕсли ИмяСобытия = "ЗакрытьМастер" Тогда
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОриентацияЭкрана

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ИмяПользователяПриИзменении(Элемент)
	
	ТекстОтвет = "";
	ОчиститьСообщения();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресЭлектроннойПочтыПриИзменении(Элемент)
	
	ТекстОтвет = "";
	ОчиститьСообщения();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаПриИзменении(Элемент)
	
	ТекстОтвет = "";
	ОчиститьСообщения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Зарегистрироваться(Команда)
	
	ТекстОтвет = "";
	ОчиститьСообщения();
	
	Если НЕ ПроверитьЗаполнениеФормы() Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиПользователя();
	
	Оповещение = Новый ОписаниеОповещения("ЗарегистрироватьсяЗавершение", ЭтотОбъект);
	РегистрацияВОблакеКлиент.ЗарегистрироватьПользователя(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьМастер(Команда)
	
	РегистрацияВОблакеКлиент.ЗакрытьМастер();
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаПредупреждение;
	
	
КонецПроцедуры

&НаКлиенте
Процедура MailRu(Команда)
	
	ДобавитьКАдресуEMAIL("@mail.ru");
	
КонецПроцедуры

&НаКлиенте
Процедура RamblerRu(Команда)
	
	ДобавитьКАдресуEMAIL("@rambler.ru");
	
КонецПроцедуры

&НаКлиенте
Процедура YahooCom(Команда)
	
	ДобавитьКАдресуEMAIL("@yahoo.com");
	
КонецПроцедуры

&НаКлиенте
Процедура YandexRu(Команда)
	
	ДобавитьКАдресуEMAIL("@yandex.ru");
	
КонецПроцедуры

&НаКлиенте
Процедура YaRu(Команда)
	
	ДобавитьКАдресуEMAIL("@ya.ru");
	
КонецПроцедуры

&НаКлиенте
Процедура GmailCom(Команда)
	
	ДобавитьКАдресуEMAIL("@gmail.com");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗарегистрироватьсяЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Успешно Тогда
		
		ОтобразитьОшибку(Результат.ТекстОшибки);
		Возврат;
		
	КонецЕсли;
	
	ОткрытьФорму("Обработка.РегистрацияВОблаке.Форма.ПродолжениеРегистрацииФорма1",,
		ЭтотОбъект,
		УникальныйИдентификатор
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКАдресуEMAIL(ПостфиксАдреса)
	
	// удалить текущий постфикс
	ПозицияСимвола = СтрНайти(АдресЭлектроннойПочты, "@");
	
	Если ПозицияСимвола > 0 Тогда
		АдресЭлектроннойПочты = Лев(АдресЭлектроннойПочты, ПозицияСимвола - 1);
	КонецЕсли;
	
	АдресЭлектроннойПочты = АдресЭлектроннойПочты + ПостфиксАдреса;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	ОбщегоНазначения.УстановитьЖирныйШрифтПолей(ЭтотОбъект, "Зарегистрироваться");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьОшибку(ТекстСообщения)
	
	Сообщить(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеФормы()
	
	Если НЕ ЗначениеЗаполнено(ИмяПользователя) Тогда
		
		ПомощникUIКлиент.СообщитьПолеНеЗаполнено(
			ЭтотОбъект,
			Элементы.ИмяПользователя,
			НСтр("ru = 'Имя пользователя'")
		);
		
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(АдресЭлектроннойПочты) Тогда
		
		ПомощникUIКлиент.СообщитьПолеНеЗаполнено(
			ЭтотОбъект,
			Элементы.АдресЭлектроннойПочты,
			НСтр("ru = 'Адрес электронной почты'")
		);
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура СохранитьНастройкиПользователя()
	
	РегистрацияВОблакеВызовСервера.СохранитьИмяПользователя(ИмяПользователя);
	РегистрацияВОблакеВызовСервера.СохранитьEmailПользователя(АдресЭлектроннойПочты);
	РегистрацияВОблакеВызовСервера.СохранитьНомерТелефонаПользователя(НомерТелефона);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиПользователя()
	
	ИмяПользователя = РегистрацияВОблакеВызовСервера.ПолучитьИмяПользователя();
	АдресЭлектроннойПочты = РегистрацияВОблакеВызовСервера.ПолучитьEmailПользователя();
	НомерТелефона = РегистрацияВОблакеВызовСервера.ПолучитьНомерТелефонаПользователя();
	
КонецПроцедуры

#КонецОбласти





