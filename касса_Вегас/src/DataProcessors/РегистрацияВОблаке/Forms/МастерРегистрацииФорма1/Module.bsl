
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыМастера = РегистрацияВОблакеКлиентСерверПовтИсп.ПолучитьПараметрыМастераРегистрации();
	
	НастроитьФормуПоЗначениямНастроек();
	
	ЗаполнитьОписание();
	
	Если НЕ ЗначениеЗаполнено(РегистрацияВОблакеВызовСервера.ПолучитьСтатусРегистрации()) Тогда
		
		РегистрацияВОблакеВызовСервера.СохранитьСтатусРегистрации(
			Перечисления.СтатусыРегистрацииВОблаке.ОткрытМастер
		);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыЗначенияНастроек" ИЛИ ИмяСобытия = "ВыполненОбмен" Тогда
		
		НастроитьФормуПоЗначениямНастроек();
		ЗаполнитьОписание();
		
	ИначеЕсли ИмяСобытия = "ЗакрытьМастер" Тогда
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОриентацияЭкрана

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТекстПриветствияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Обработка.РегистрацияВОблаке.Форма.Подробнее1СКасса",, ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКРегистрации(Команда)
	
	ОткрытьФорму("Обработка.РегистрацияВОблаке.Форма.МастерРегистрацииФорма2",,
		ЭтотОбъект,
		УникальныйИдентификатор
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьМастер(Команда)
	
	РегистрацияВОблакеКлиент.ЗакрытьМастер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОписание()
	
	Содержимое1 = НСтр("ru = 'Подключитесь бесплатно к облачному сервису 1С:Касса и получите больше возможностей для ведения бизнеса:
		|
		|• Удобная работа с товарами и ценами
		|• Простой товароучет, склад и остатки
		|• Полноценный учет ЕГАИС 3.0
		|• Данные о продажах с нескольких касс
		|• Развернутая аналитика: более 20 отчетов
		|• Выгрузка данных в 1С:Бухгалтерию 8
		|и многое другое'"
	);
	
	Содержимое2 = Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее о 1С:Кассе'"),
		,,, "Ссылка"
	);
	
	Содержимое3 = НСтр("ru = 'Зарегистрируйтесь в сервисе 1С:Фреш и начните работать в 1С:Кассе'");
	
	МассивОписания = Новый Массив;
	МассивОписания.Добавить(Содержимое1);
	
	МассивОписания.Добавить(Символы.ПС);
	МассивОписания.Добавить(Символы.ПС);
	
	МассивОписания.Добавить(Содержимое2);
	
	МассивОписания.Добавить(Символы.ПС);
	МассивОписания.Добавить(Символы.ПС);
	
	МассивОписания.Добавить(Содержимое3);
	
	ОписаниеФорматированнаяСтрока = Новый ФорматированнаяСтрока(МассивОписания, ШрифтПриложения);
	
	Элементы.Описание.Заголовок = ОписаниеФорматированнаяСтрока;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ШрифтПриложения = ЗначениеНастроекПовтИсп.ШрифтПриложения();
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	ОбщегоНазначения.УстановитьЖирныйШрифтПолей(ЭтотОбъект, "Регистрация");
	
КонецПроцедуры

#КонецОбласти


