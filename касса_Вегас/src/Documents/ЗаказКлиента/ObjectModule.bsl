
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Проведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ЗаказКлиента.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	Проведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Продажи.ОтразитьРасчетыСКлиентами(ДополнительныеСвойства, Движения, Отказ);
	
	Проведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	Проведение.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЦенаВключаетНДС = ЗначениеНастроекПовтИсп.ЦенаВключаетНДС();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуДокумента();
	
	Если НЕ ЗначениеЗаполнено(НомерЗаказа) Тогда
		НомерЗаказа = Номер;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтатусЗаказа) Тогда
		СтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	СтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьСуммуДокумента()
	
	СуммаДокумента = Товары.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли