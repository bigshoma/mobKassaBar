
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Чек = Параметры.Чек;
	
	ЗаполнитьДанные(Чек);
	
	НастроитьВидимостьПолей();
	
	НастроитьФормуПоЗначениямНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанные(Чек)
	
	РеквизитыЧека = ОбщегоНазначенияВызовСервера.ЗначенияРеквизитовОбъекта(Чек, 
		"ФискальныйНомер,
		|ШтрихкодЧека,
		|ПризнакСпособаРасчета,
		|Дата,
		|АдресЧекаЕГАИС,
		|ПодписьЧекаЕГАИС,
		|ФискальныйПризнак,
		|СистемаНалогообложенияККТ,
		|КассоваяСмена"
	);
	
	
	Если ЗначениеЗаполнено(РеквизитыЧека.СистемаНалогообложенияККТ) Тогда
		
		СНО = ПолучитьПредставлениеСНО(РеквизитыЧека.СистемаНалогообложенияККТ);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыЧека.КассоваяСмена) Тогда
		
		НомерСмены = ПродажиВызовСервера.ПолучитьНомерСменыДляПечати(РеквизитыЧека.КассоваяСмена, Ложь);
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЧека,
		"ФискальныйНомер,
		|ШтрихкодЧека,
		|ПризнакСпособаРасчета,
		|Дата,
		|АдресЧекаЕГАИС,
		|ПодписьЧекаЕГАИС,
		|ФискальныйПризнак
		|"
	);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПредставлениеСНО(СистемаНалогообложенияККТ)
	
	ПредставлениеСНО = "";
	
	Если СистемаНалогообложенияККТ = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход Тогда
		
		ПредставлениеСНО = НСтр("ru = 'УСН доход'");
		
	ИначеЕсли СистемаНалогообложенияККТ = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход Тогда
		
		ПредставлениеСНО = НСтр("ru = 'УСН доход-расход'");
		
	ИначеЕсли СистемаНалогообложенияККТ = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД Тогда
		
		ПредставлениеСНО = НСтр("ru = 'ЕНВД'");
		
	ИначеЕсли СистемаНалогообложенияККТ = Перечисления.ТипыСистемНалогообложенияККТ.ЕСН Тогда
		
		ПредставлениеСНО = НСтр("ru = 'ЕСН'");
		
	ИначеЕсли СистемаНалогообложенияККТ = Перечисления.ТипыСистемНалогообложенияККТ.Патент Тогда
		
		ПредставлениеСНО = НСтр("ru = 'Патент'");
	Иначе
		
		ПредставлениеСНО = НСтр("ru = 'ОСН'");
	КонецЕсли;
	
	Возврат ПредставлениеСНО;
	
КонецФункции

&НаСервере
Процедура НастроитьВидимостьПолей()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтотОбъект,
		"СНО",
		"Видимость",
		ЗначениеЗаполнено(СНО)
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтотОбъект,
		"НомерСмены",
		"Видимость",
		ЗначениеЗаполнено(НомерСмены)
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтотОбъект,
		"ФискальныйНомер",
		"Видимость",
		ЗначениеЗаполнено(ФискальныйНомер)
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтотОбъект,
		"ФискальныйПризнак",
		"Видимость",
		ЗначениеЗаполнено(ФискальныйПризнак)
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтотОбъект,
		"ШтрихкодЧека",
		"Видимость",
		ЗначениеЗаполнено(ШтрихкодЧека)
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтотОбъект,
		"АдресЧекаЕГАИС",
		"Видимость",
		ЗначениеЗаполнено(АдресЧекаЕГАИС)
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтотОбъект,
		"ПодписьЧекаЕГАИС",
		"Видимость",
		ЗначениеЗаполнено(ПодписьЧекаЕГАИС)
	);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
