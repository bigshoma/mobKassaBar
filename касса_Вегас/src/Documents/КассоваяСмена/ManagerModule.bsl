
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекстЗаголовка = НСтр("ru = 'Кассовая смена %ПредставлениеНомера% от %ПредставлениеДата%'");
	
	Если ЗначениеЗаполнено(Данные.Ссылка.НомерСмены) Тогда
		Номер = Данные.Ссылка.НомерСмены;
	Иначе
		Номер = Данные.Ссылка.Номер
	КонецЕсли;
	
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%ПредставлениеНомера%", ОбщегоНазначенияКлиентСервер.ПредставлениеНомера(Номер));
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%ПредставлениеДата%",   Формат(Данные.Ссылка.Дата, "ДЛФ=D"));
	
	Представление = ТекстЗаголовка;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует таблицы значений, содержащие данные документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	НаличныеОстатки.КассоваяСмена,
	|	НаличныеОстатки.СуммаОстаток КАК Сумма,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|ИЗ
	|	РегистрНакопления.Наличные.Остатки(, КассоваяСмена = &Ссылка) КАК НаличныеОстатки
	|ГДЕ
	|	НаличныеОстатки.КассоваяСмена.Закрыта";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ДокументСсылка.ОкончаниеКассовойСмены);
	
	Результат = Запрос.Выполнить();

	Результат = Запрос.ВыполнитьПакет();
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаНаличные", Результат[0].Выгрузить());
	
КонецПроцедуры

Функция КассирОткрытойСмены() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КассоваяСмена.Ссылка КАК Ссылка,
		|	КассоваяСмена.Кассир КАК Кассир
		|ИЗ
		|	Документ.КассоваяСмена КАК КассоваяСмена
		|ГДЕ
		|	КассоваяСмена.Проведен
		|	И НЕ КассоваяСмена.Закрыта";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.Кассир;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецЕсли