
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоЗначениямНастроек();
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ПараметрыСтроки);
	
	ТаблицаОрганизаций = ПолучитьИзВременногоХранилища(Параметры.ТаблицаОрганизаций);
	Для Каждого СтрокаТаблицы Из ТаблицаОрганизаций Цикл
		НоваяСтрока = Организации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;
	
	// ОриентацияЭкрана
	НастроитьФормуПоОриентацииЭкрана();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыЗначенияНастроек" Тогда
		
		НастроитьФормуПоЗначениямНастроек();
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	// ОриентацияЭкрана
	ПриИзмененииПараметровЭкранаСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ПроизводительОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоказатьОрганизацию(Производитель, НСтр("ru = 'Производитель'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьОрганизацию(Код, ЗаголовокФормыОрганизации)
	
	НайденныеСтроки = Организации.НайтиСтроки(Новый Структура("Код", Код));
	
	Если НЕ НайденныеСтроки.Количество() = 0 Тогда
		
		НайденнаяОрганизация = НайденныеСтроки[0];
		
		РеквизитыОрганизации = Новый Структура;
		РеквизитыОрганизации.Вставить("Код");
		РеквизитыОрганизации.Вставить("ИНН");
		РеквизитыОрганизации.Вставить("КодРегиона");
		РеквизитыОрганизации.Вставить("КодСтраны");
		РеквизитыОрганизации.Вставить("КПП");
		РеквизитыОрганизации.Вставить("Наименование");
		РеквизитыОрганизации.Вставить("НаименованиеПолное");
		РеквизитыОрганизации.Вставить("ПочтовыйИндекс");
		РеквизитыОрганизации.Вставить("ПредставлениеАдреса");
		
		ЗаполнитьЗначенияСвойств(РеквизитыОрганизации, НайденнаяОрганизация);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РеквизитыОрганизации", РеквизитыОрганизации);
		ПараметрыФормы.Вставить("ЗаголовокФормы", ЗаголовокФормыОрганизации);
		ОткрытьФорму("Документ.ТоварноТранспортнаяНакладнаяЕГАИС.Форма.ФормаОрганизации", ПараметрыФормы, ЭтаФорма);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ЗаголовокФормыОрганизации + Символы.НПП + НСтр("ru = 'не определен'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоЗначениямНастроек()
	
	ОбщегоНазначения.УстановитьШрифт(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОриентацияЭкрана

// ОриентацияЭкрана
&НаСервере
Процедура ПриИзмененииПараметровЭкранаСервер()
	
	ОбщегоНазначения.УстановитьОриентациюЭкрана();
	НастроитьФормуПоОриентацииЭкрана();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОриентацииЭкрана()
	
	СписокПолей = "Наименование,
		|НаименованиеПолное,
		|Количество,
		|Цена,
		|Сумма,
		|Объем,
		|Крепость,
		|КодВидаПродукции,
		|Тип,
		|Производитель,
		|НомерСправкиА,
		|НомерСправкиБ,
		|НомерСправкиБПоставщика,
		|НомерПартии,
		|Код,
		|Импортер,
		|ИдентификаторУпаковки,
		|ИдентификаторСтроки,
		|Идентификатор,
		|ДиапазоныНомеровМарок";
	
	Если ПараметрыСеанса.ГоризонтальнаяОриентацияЭкрана Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, СписокПолей, "ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Лево);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, СписокПолей, "ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Верх);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

