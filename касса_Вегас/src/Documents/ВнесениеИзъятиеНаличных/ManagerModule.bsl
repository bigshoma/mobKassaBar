
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует таблицы значений, содержащие данные документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВнесениеИзъятиеНаличных.Дата КАК Период,
	|	&ТипОплаты КАК ТипОплаты,
	|	ВнесениеИзъятиеНаличных.КассоваяСмена КАК КассоваяСмена,
	|	ВЫБОР
	|		КОГДА ВнесениеИзъятиеНаличных.ВидДвиженияНаличных = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияДС.Внесение)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ВнесениеИзъятиеНаличных.Сумма,
	|	ВнесениеИзъятиеНаличных.ВидДвиженияНаличных КАК ВидДвиженияДС
	|ИЗ
	|	Документ.ВнесениеИзъятиеНаличных КАК ВнесениеИзъятиеНаличных
	|ГДЕ
	|	ВнесениеИзъятиеНаличных.Ссылка = &Ссылка";
	
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ТипОплаты", Перечисления.ТипыОплаты.Наличные);
	
	Результат = Запрос.ВыполнитьПакет();
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредства", Результат[0].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли